load(
    "//src/keycloak:config.bzl",
    "KEYCLOAK_ENTRYPOINTS",
    "KEYCLOAK_MARKDOWN_TARGETS",
    "KEYCLOAK_TARGETS",
    "KEYCLOAK_TESTING_TARGETS",
)
load(
    "//tools:defaults.bzl",
    "markdown_to_html",
    "ng_module",
    "ng_package",
    "ng_test_library",
    "ng_web_test_suite",
)

package(default_visibility = ["//visibility:public"])

# Root "@sbb-esta/angular-keycloak" entry-point.
ng_module(
    name = "keycloak",
    srcs = glob(
        [
            "*.ts",
            "src/**/*.ts",
        ],
        exclude = ["**/*.spec.ts"],
    ),
    module_name = "@sbb-esta/angular-keycloak",
    deps = ["//src/keycloak/%s" % p for p in KEYCLOAK_ENTRYPOINTS] + [
        "@npm//@angular/common",
        "@npm//@angular/core",
        "@npm//keycloak-js",
        "@npm//rxjs",
    ],
)

ng_test_library(
    name = "unit_test_sources",
    srcs = glob(
        ["src/**/*.spec.ts"],
        exclude = ["src/**/*.e2e.spec.ts"],
    ),
    deps = [
        ":keycloak",
        "@npm//@angular/common",
        "@npm//keycloak-js",
        "@npm//rxjs",
    ],
)

ng_web_test_suite(
    name = "unit_tests",
    deps = [":unit_test_sources"],
)

markdown_to_html(
    name = "overview",
    srcs = [
        ":README.md",
    ],
)

filegroup(
    name = "overviews",
    srcs = [":overview"] + ["//src/keycloak/%s:overview" % name for name in KEYCLOAK_MARKDOWN_TARGETS],
)

# Creates the @sbb-esta/angular-keycloak package published to npm.
ng_package(
    name = "npm_package",
    srcs = [
        "package.json",
    ],
    data = [
    ],
    entry_point = ":public-api.ts",
    entry_point_name = "keycloak",
    readme_md = "README.md",
    tags = ["release-package"],
    deps = KEYCLOAK_TARGETS + KEYCLOAK_TESTING_TARGETS,
)
