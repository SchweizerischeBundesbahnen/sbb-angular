load("@npm//@bazel/typescript:index.bzl", "ts_config")
load("//src/angular:config.bzl", "ANGULAR_ENTRYPOINTS", "ANGULAR_TESTING_ENTRYPOINTS")
load("//src/angular-business:config.bzl", "BUSINESS_ENTRYPOINTS", "BUSINESS_TESTING_ENTRYPOINTS")
load("//src/angular-core:config.bzl", "CORE_ENTRYPOINTS", "CORE_TESTING_ENTRYPOINTS")
load("//src/angular-maps:config.bzl", "MAPS_ENTRYPOINTS", "MAPS_TESTING_ENTRYPOINTS")
load("//src/angular-public:config.bzl", "PUBLIC_ENTRYPOINTS", "PUBLIC_TESTING_ENTRYPOINTS")
load("//tools:defaults.bzl", "ts_library")
load("//tools/dgeni:index.bzl", "dgeni_api_docs")

package(default_visibility = ["//visibility:public"])

exports_files([
    "bazel-tsconfig-build.json",
    "bazel-tsconfig-test.json",
    "README.md",
])

ts_config(
    name = "tsconfig-test",
    src = "bazel-tsconfig-test.json",
    deps = ["bazel-tsconfig-build.json"],
)

angular_api_entry_points = ANGULAR_ENTRYPOINTS + ANGULAR_TESTING_ENTRYPOINTS

business_api_entry_points = BUSINESS_ENTRYPOINTS + BUSINESS_TESTING_ENTRYPOINTS

core_api_entry_points = CORE_ENTRYPOINTS + CORE_TESTING_ENTRYPOINTS

maps_api_entry_points = MAPS_ENTRYPOINTS + MAPS_TESTING_ENTRYPOINTS

public_api_entry_points = PUBLIC_ENTRYPOINTS + PUBLIC_TESTING_ENTRYPOINTS

# List that contains all source files that need to be processed by Dgeni.
api_source_files = ["//src/angular/%s:source-files" % name for name in angular_api_entry_points] + \
                   ["//src/angular-business/%s:source-files" % name for name in business_api_entry_points] + \
                   ["//src/angular-core/%s:source-files" % name for name in core_api_entry_points] + \
                   ["//src/angular-maps/%s:source-files" % name for name in maps_api_entry_points] + \
                   ["//src/angular-public/%s:source-files" % name for name in public_api_entry_points]

dgeni_api_docs(
    name = "api-docs",
    srcs = api_source_files + [
        # Add all Angular packages to the sources because some Material exports use
        # Angular exports and these should not cause any warnings when Dgeni uses the
        # type checker to parse our TypeScript sources.
        "@npm//@angular/cdk",
        "@npm//@angular/core",
        "@npm//@angular/common",
        "@npm//@angular/forms",
        "@npm//@angular/animations",
        "@npm//@angular/platform-browser",
    ],
    entry_points = {
        "angular": angular_api_entry_points,
        "angular-business": business_api_entry_points,
        "angular-core": core_api_entry_points,
        "angular-maps": maps_api_entry_points,
        "angular-public": public_api_entry_points,
    },
    tags = ["docs-package"],
)

ts_library(
    name = "dev_mode_types",
    srcs = ["dev-mode-types.d.ts"],
)
