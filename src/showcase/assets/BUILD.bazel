load("//src/angular-business:config.bzl", "BUSINESS_ENTRYPOINTS")
load("//src/angular-public:config.bzl", "PUBLIC_ENTRYPOINTS")
load("//tools/package-docs-content:index.bzl", "package_docs_content")

package(default_visibility = ["//visibility:public"])

filegroup(
    name = "assets",
    srcs = glob([
        "**/*.svg",
        "**/*.png",
        "**/*.jpg",
        "**/*.ico",
        "**/*.html",
        "**/*.json",
        "**/*.ts",
        "**/*.css",
        "**/*.ts",
    ]) + [
        ":docs-content",
        ":stackblitz/src/app/sbb-public.module.ts",
        ":stackblitz/src/app/sbb-business.module.ts",
    ],
    data = [":docs-content"],
)

package_docs_content(
    name = "docs-content",
    srcs = {
        # Package the overviews into the docs content
        "//src/angular-business:overviews": "overviews/business",
        "//src/angular-core:overviews": "overviews/core",
        "//src/angular-icons:overviews": "overviews/icons",
        "//src/angular-keycloak:overviews": "overviews/keycloak",
        "//src/angular-maps:overviews": "overviews/maps",
        "//src/angular-public:overviews": "overviews/public",

        # In order to be able to run examples in StackBlitz, we also want to package the
        # plain source files into the docs-content.
        "//src/showcase:example-source-files": "examples-source",

        # For the live-examples in our docs, we want to package the highlighted files
        # into the docs content. These will be used to show the source code for examples.
        "//src/showcase:highlighted-source-files": "examples-highlighted",

        # Package the API docs into the docs-content
        "//src:api-docs": "",
    },
    tags = ["docs-package"],
)

genrule(
    name = "sbb_module_public",
    outs = ["stackblitz/src/app/sbb-public.module.ts"],
    cmd = """
      # Run the bazel entry-point for generating the sbb modules
      # for the examples on StackBlitz info.
      ./$(execpath //tools/stackblitz-module-template:bazel-bin) \
          "$(execpath stackblitz/src/app/sbb-public.module.ts)" \
          %s
    """ % " ".join(PUBLIC_ENTRYPOINTS),
    output_to_bindir = True,
    tools = ["//tools/stackblitz-module-template:bazel-bin"],
)

genrule(
    name = "sbb_module_business",
    outs = ["stackblitz/src/app/sbb-business.module.ts"],
    cmd = """
      # Run the bazel entry-point for generating the sbb modules
      # for the examples on StackBlitz info.
      ./$(execpath //tools/stackblitz-module-template:bazel-bin) \
          "$(execpath stackblitz/src/app/sbb-business.module.ts)" \
          %s
    """ % " ".join(BUSINESS_ENTRYPOINTS),
    output_to_bindir = True,
    tools = ["//tools/stackblitz-module-template:bazel-bin"],
)
